module sugar/rules

  imports
    core/identifiers
    core/signatures
    core/terms
    core/strategies
    sugar/terms
    sugar/strategies

sorts RuleDef

template options
  tokenize: ")(|"

context-free syntax
  RuleDef.RDefNoArgs =
<<Id>:
  <Rule>>

  Def.AnnoDef =
    <<Anno+> <RuleDef>>

  RuleDef.RDef =
<<Id>(<{Typedid ", "}*>):
  <Rule>>

  RuleDef.RDefT =
<<Id>(<{Typedid ", "}*> | <{Typedid ", "}*>):
  <Rule>>

sorts Rule RuleCond

context-free syntax
//    Rule.RuleNoCond = [[Term] -> [Term]] // backward comp.
//
//    Rule.Rule = [[Term] -> [Term] where [Strategy]]  // backward comp.

  Rule.Rule = [[Term] -> [Term] [RuleCond*]] //{avoid}

  RuleCond.WhereClause =
<where
  <Strategy>>
  RuleCond.WithClause =
<with
  <Strategy>>


