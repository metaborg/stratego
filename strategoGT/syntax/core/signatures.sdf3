module core/signatures

imports
  core/identifiers
  core/constants

sorts Sdecl
context-free syntax

  Sdecl.Sorts =
    <sorts <Sort*>>

  Sdecl.Constructors =
    <constructors
       <Opdecl*>>

sorts Sort
context-free syntax
  Sort.SortVar    = <<LCID>>
  Sort.SortNoArgs = <<UCID>>
  Sort.Sort       = <<Id>(<{Sort ","}*>)>

sorts Opdecl
context-free syntax
  Opdecl.OpDecl    = <<Id> : <OpType>>
  Opdecl.OpDeclQ   = <<String> : <OpType>>
  Opdecl.OpDeclInj = <: <OpType>>

  Opdecl.ExtOpDecl    = <external <Id> : <OpType>>
  Opdecl.ExtOpDeclQ   = <external <String> : <OpType>>
  Opdecl.ExtOpDeclInj = <external : <OpType>>

sorts OldType OldArgType ConstType FunType RetType OpType
context-free syntax
  ConstType.ConstType = <<Sort>>

  FunType.FunType = [[{OldArgType "*"}+] -> [RetType]]

  OldArgType = <(<OldType>)> {bracket}
  OldArgType = <<ConstType>>
  RetType = <<OldType>>
  RetType = <<FunType>> {reject}

  OldType = <<FunType>>
  OldType = <<ConstType>>

  OpType = ConstType
  OpType.FunType = [[{ConstType "*"}+] -> [ConstType]]

// Restriction is required for the Sort* in Sdecl: List(a) is ambiguous.
context-free restrictions
  Sort -/- [\(]
