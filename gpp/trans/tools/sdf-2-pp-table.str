module sdf-2-pp-table
imports
  sdf-2-pp-lib
  Sdf2
  Bracket-Symbol

strategies

  /**
   * A pretty-print table is generated by collecting all context-free
   * prodcutions from an sdf module or definition and generating BOX templates
   * for each production and, recursively for each nested symbol of the
   * production.
   */
  ppgenerate = 
      // Obtain all context-free productions
      collect(?context-free-syntax(<id>))
    ; concat

      // De-quote all strings
    ; topdown(
        try(
          cons(un-double-quote)
        + lit( un-double-quote)
        + sort(un-double-quote)
        + \ lit(s) -> lit(<un-double-quote> s) \
        )
      )
   
      // Create BOX templates for each production
    ; map(generate-pp-entries)
    ; filter( not(?[]) )
    ; concat => pp-entries

    // Construct a valid AST for the pretty-print table
    ; !PP-Table(pp-entries)

    ; topdown(
        try(
          Arg(int-to-string)
        + selector(int-to-string, id)
        + S(double-quote)
        )
      )
