module lib-pp-table-args
imports
  libstratego-gpp
  
strategies

  pptable-option =
    ArgOption("-p"
    , where(<extend-config> ("-p", [<id>]))
    , !"-p file          Use pretty-print table in file"
    )

  /**
   * Specifying pretty-print tables is optional, so the default is the empty list.
   */   
  get-pptables-config =
    <get-config> "-p" <+ ![]

strategies

  read-pptables-config =
    get-pptables-config
    ; map(read-pptable-file)
    
  read-pptable-file =
      if has-extension(|"af") then
        ReadFromFile
      else
        if has-extension(|"pp") then
          lookup-parsed-pptable-arg
          <+ parse-pptable-arg
        else
          log(|Error(), ["Pretty-print table must have extension .pp.af or .pp :", <id>])
          ; <exit> 1
        end
      end

  lookup-parsed-pptable-arg =
      !(<add-extension; file-exists> (<id>, "af"), <id>)
    ; where((modification-time, modification-time); geq)
    ; Fst
    ; log(|Notice(),<concat-strings> ["Using existing parsed pretty print-table: ",<id>])
    ; ReadFromFile

  parse-pptable-arg =
    parse-pptable-file
    ; log(|Notice(), "Parsing pretty-print-table")
