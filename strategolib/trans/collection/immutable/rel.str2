/**
 * This module contains strategies for operating on immutable binary relations.
 *
 * Note that these relations do not support generic traversal. These are "blob" terms without children
 * and need to be operated on through the strategies in this module.
 *
 * @author Jeff Smits <mail@jeffsmits.net>
 *
 */
module collection/immutable/rel
imports
  strategy/conditional
  collection/list/cons
  collection/tuple/cons
  collection/immutable/set
  collection/immutable/map

signature
  sorts
    ImmutableRelation(*, *)
    ImmutableRelationImplBlob
  constructors
    /**
     * Represents a reference to a Set
     */
    ImmutableRelation : ImmutableRelationImplBlob -> ImmutableRelation(k, v)
    : blob -> ImmutableRelationImplBlob

/**
 * Immutable relation construction and deconstruction
 */
strategies

external internal-immutable-relation :: (|) ? -> ImmutableRelationImplBlob

/**
 * Create an empty immutable relation.
 *
 * @type _ -> ImmutableRelation
 */
imrel-new :: a -> ImmutableRelation(k, v)
imrel-new = internal-immutable-relation; !ImmutableRelation(<id>)

external internal-immutable-relation-from-list :: (|) List(k * v) -> ImmutableRelationImplBlob

/**
 * Create an immutable relation from a list of pairs.
 *
 * @type List -> ImmutableRelation
 */
imrel-from-list :: List(k * v) -> ImmutableRelation(k, v)
imrel-from-list = internal-immutable-relation-from-list; !ImmutableRelation(<id>)

external internal-immutable-relation-to-list :: (|) ImmutableRelationImplBlob -> List(k * v)

/**
 * Turn the immutable relation into a list of pairs.
 *
 * @type ImmutableRelation -> List
 */
imrel-to-list :: ImmutableRelation(k, v) -> List(k * v)
imrel-to-list = ?ImmutableRelation(<id>); internal-immutable-relation-to-list

external internal-immutable-relation-to-map :: (v * v -> v|) ImmutableRelationImplBlob -> ImmutableMapImplBlob

/**
 * Turn the immutable relation into a map.
 * For pairs with the same key, the values are merged with the second parameter.
 *
 * @type ImmutableRelation -> ImmutableMap
 */
imrel-to-map(v * v -> v) :: ImmutableRelation(k, v) -> ImmutableMap(k, v)
imrel-to-map(merge) = ?ImmutableRelation(<id>); internal-immutable-relation-to-map(merge|); !ImmutableMap(<id>)

/**
 * Turn the immutable relation into a list of pairs.
 *
 * @type ImmutableRelation -> List
 */
imrel-pairs :: ImmutableRelation(k, v) -> List(k * v)
imrel-pairs = imrel-to-list

external internal-immutable-relation-to-set :: (|) ImmutableRelationImplBlob -> ImmutableSetImplBlob

/**
 * Turn the immutable relation into a set of pairs.
 *
 * @type ImmutableRelation -> ImmutableSet
 */
imrel-pairs-imset :: ImmutableRelation(k, v) -> ImmutableSet(k * v)
imrel-pairs-imset = ?ImmutableRelation(<id>); internal-immutable-relation-to-set; !ImmutableSet(<id>)

external internal-immutable-relation-keys :: (|) ImmutableRelationImplBlob -> List(k)

/**
 * Get all keys of the immutable relation.
 *
 * @type ImmutableRelation -> List
 */
imrel-keys :: ImmutableRelation(k, v) -> List(k)
imrel-keys = ?ImmutableRelation(<id>); internal-immutable-relation-keys

external internal-immutable-relation-keys-set :: (|) ImmutableRelationImplBlob -> ImmutableSetImplBlob

/**
 * Get all keys of the immutable relation as an immutable set.
 *
 * @type ImmutableRelation -> List
 */
imrel-keys-imset :: ImmutableRelation(k, v) -> ImmutableSet(k)
imrel-keys-imset = ?ImmutableRelation(<id>); internal-immutable-relation-keys-set; !ImmutableSet(<id>)

external internal-immutable-relation-values :: (|) ImmutableRelationImplBlob -> List(v)

/**
 * Get all values of the immutable relation.
 *
 * @type ImmutableRelation -> List
 */
imrel-values :: ImmutableRelation(k, v) -> List(v)
imrel-values = ?ImmutableRelation(<id>); internal-immutable-relation-values

external internal-immutable-relation-values-set :: (|) ImmutableRelationImplBlob -> ImmutableSetImplBlob

/**
 * Get all values of the immutable relation.
 *
 * @type ImmutableRelation -> ImmutableSet
 */
imrel-values-imset :: ImmutableRelation(k, v) -> ImmutableSet(v)
imrel-values-imset = ?ImmutableRelation(<id>); internal-immutable-relation-values-set; !ImmutableSet(<id>)

external internal-immutable-relation-transitive-closure :: (|) ImmutableRelationImplBlob -> ImmutableRelationImplBlob

/**
 * Take the transitive closure of the relation.
 *
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-transitive-closure :: ImmutableRelation(k, k) -> ImmutableRelation(k, k)
imrel-transitive-closure = ImmutableRelation(internal-immutable-relation-transitive-closure)

external internal-immutable-relation-reflexive-transitive-closure :: (|) ImmutableRelationImplBlob -> ImmutableRelationImplBlob

/**
 * Take the reflexive transitive closure of the relation.
 *
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-reflexive-transitive-closure :: ImmutableRelation(k, k) -> ImmutableRelation(k, k)
imrel-reflexive-transitive-closure =
  ImmutableRelation(internal-immutable-relation-reflexive-transitive-closure)

external internal-immutable-relation-inverse :: (|) ImmutableRelationImplBlob -> ImmutableRelationImplBlob

/**
 * Invert the relation (swap all the pairs).
 *
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-invert :: ImmutableRelation(k, v) -> ImmutableRelation(v, k)
imrel-invert: ImmutableRelation(b) ->
  ImmutableRelation(<internal-immutable-relation-inverse> b)

/**
 * Elemental operations
 */
strategies

/**
 * Succeeds if the given immutable relation is empty.
 *
 * @type ImmutableRelation -?> ImmutableRelation
 */
imrel-empty :: ImmutableRelation(k, v) -> ImmutableRelation(k, v)
imrel-empty = equal(|<imrel-new>)

/**
 * Succeeds if the key is in the immutable relation.
 *
 * @param k - the key
 * @type ImmutableRelation -?> ImmutableRelation
 */
imrel-contains-key(|k) :: ImmutableRelation(k, v) -> ImmutableRelation(k, v)
imrel-contains-key(|k) = where(imrel-get(|k); not(imset-empty))

external internal-immutable-relation-contains :: (|k, v) ImmutableRelationImplBlob -> ImmutableRelationImplBlob

/**
 * Succeeds if the key is in the immutable relation.
 *
 * @param k - the key
 * @type ImmutableRelation -?> ImmutableRelation
 */
imrel-contains(|k, v) :: ImmutableRelation(k, v) -> ImmutableRelation(k, v)
imrel-contains(|k, v) = ImmutableRelation(internal-immutable-relation-contains(|k, v))

external internal-immutable-relation-get :: (|k) ImmutableRelationImplBlob -> ImmutableSetImplBlob

/**
 * Returns the associated value of the key in the immutable relation,
 * or fails if the key is not in the immutable relation.
 *
 * @param e - the key used to look up the value
 * @type ImmutableRelation -?> ImmutableSet
 */
imrel-get(|k) :: ImmutableRelation(k, v) -> ImmutableSet(v)
imrel-get(|k) = ?ImmutableRelation(<id>); internal-immutable-relation-get(|k); !ImmutableSet(<id>)

external internal-immutable-relation-insert :: (|k, v) ImmutableRelationImplBlob -> ImmutableRelationImplBlob

/**
 * Inserts an element in the immutable relation. Returns a new relation with the added element.
 *
 * @param e - the element that is inserted
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-insert(|k, v) :: ImmutableRelation(k, v) -> ImmutableRelation(k, v)
imrel-insert(|k, v) = ImmutableRelation(internal-immutable-relation-insert(|k, v))

external internal-immutable-relation-remove :: (|k) ImmutableRelationImplBlob -> ImmutableRelationImplBlob

/**
 * Removes an element in the immutable relation. Returns a new relation without the removed element.
 *
 * @param e - the element that is removed
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-remove(|k) :: ImmutableRelation(k, v) -> ImmutableRelation(k, v)
imrel-remove(|k) = ImmutableRelation(internal-immutable-relation-remove(|k))

/**
 * Higher-order operations
 */
strategies

external internal-immutable-relation-filter :: (k1 * v1 -> k2 * v2|) ImmutableRelationImplBlob -> ImmutableRelationImplBlob

/**
 * Remove all pairs from the immutable relation for which the filter strategy fails.
 * Returns a new immutable relation without the removed pairs.
 *
 * @param s: Term * Term -?> Term * Term
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-filter(k1 * v1 -> k2 * v2) :: ImmutableRelation(k1, v1) -> ImmutableRelation(k2, v2)
imrel-filter(s): ImmutableRelation(b) -> ImmutableRelation(<internal-immutable-relation-filter(s|)> b)

/**
 * Remove all pairs from the immutable relation for which the filter strategy fails on the key.
 * Returns a new immutable relation without the removed pairs.
 *
 * @param s: Term -?> Term
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-filter-keys(k1 -> k2) :: ImmutableRelation(k1, v) -> ImmutableRelation(k2, v)
imrel-filter-keys(s) = imrel-filter((s, id))

/**
 * Remove all pairs from the immutable relation for which the filter strategy fails on the value.
 * Returns a new immutable relation without the removed pairs.
 *
 * @param s: Term -?> Term
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-filter-values(v1 -> v2) :: ImmutableRelation(k, v1) -> ImmutableRelation(k, v2)
imrel-filter-values(s) = imrel-filter((id, s))

external internal-immutable-relation-map :: (k1 * v1 -> k2 * v2|) ImmutableRelationImplBlob -> ImmutableRelationImplBlob

/**
 * Map all pairs from the immutable relation.
 * Returns a new immutable relation with the mapped pairs.
 *
 * @param s: Term * Term -?> Term * Term
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-map(k1 * v1 -> k2 * v2) :: ImmutableRelation(k1, v1) -> ImmutableRelation(k2, v2)
imrel-map(s): ImmutableRelation(b) -> ImmutableRelation(<internal-immutable-relation-map(s|)> b)

/**
 * Map all keys from the immutable relation.
 * Returns a new immutable relation with the mapped keys.
 *
 * @param s: Term -> Term
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-map-keys(k1 -> k2) :: ImmutableRelation(k1, v) -> ImmutableRelation(k2, v)
imrel-map-keys(s) = imrel-map((s, id))

/**
 * Map all values from the immutable relation.
 * Returns a new immutable relation with the mapped values.
 *
 * @param s: Term -> Term
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-map-values(v1 -> v2) :: ImmutableRelation(k, v1) -> ImmutableRelation(k, v2)
imrel-map-values(s) = imrel-map((id, s))

/**
 * Combining operations
 */
strategies

external internal-immutable-relation-compose :: (|ImmutableRelationImplBlob) ImmutableRelationImplBlob -> ImmutableRelationImplBlob

/**
 * Compose with another immutable relation.
 * Returns a new immutable relation with all pairs (x,z) where there is a y such that (x,y) in this
 * and (y,z) in other.
 *
 * @param other: ImmutableRelation
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-compose(|ImmutableRelation(k2, k3)) :: ImmutableRelation(k1, k2) -> ImmutableRelation(k1, k3)
imrel-compose(|r): ImmutableRelation('one) -> ImmutableRelation(<internal-immutable-relation-compose(|other)> 'one)
  where ImmutableRelation(other) := r

external internal-immutable-relation-intersect :: (|ImmutableRelationImplBlob) ImmutableRelationImplBlob -> ImmutableRelationImplBlob

/**
 * Intersect with another immutable relation.
 * Returns a new immutable relation with the pairs that are in both relations.
 *
 * @param other: ImmutableRelation
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-intersect(|ImmutableRelation(k, v)) :: ImmutableRelation(k, v) -> ImmutableRelation(k, v)
imrel-intersect(|r) = where(ImmutableRelation(other) := r);
  ImmutableRelation(internal-immutable-relation-intersect(|other))

/**
 * Intersect two immutable relations.
 * Returns a new immutable relation with the pairs that are in both relations.
 *
 * @type ImmutableRelation * ImmutableRelation -> ImmutableRelation
 */
imrel-intersect :: ImmutableRelation(k, v) * ImmutableRelation(k, v) -> ImmutableRelation(k, v)
imrel-intersect =
  ?(l, r); <imrel-intersect(|r)> l

external internal-immutable-relation-subtract :: (|ImmutableRelationImplBlob) ImmutableRelationImplBlob -> ImmutableRelationImplBlob

/**
 * Subtract another immutable relation from the given one.
 * Returns a new immutable relation without the pairs in the other immutable relation.
 *
 * @param other: ImmutableRelation - the relation to remove from the current term immutable relation
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-subtract(|ImmutableRelation(k, v)) :: ImmutableRelation(k, v) -> ImmutableRelation(k, v)
imrel-subtract(|r) = where(ImmutableRelation(other) := r);
  ImmutableRelation(internal-immutable-relation-subtract(|other))

/**
 * Subtract the right immutable relation from the left.
 * Returns a new immutable relation without the pairs in the other immutable relation.
 *
 * @type ImmutableRelation * ImmutableRelation -> ImmutableRelation
 */
imrel-subtract :: ImmutableRelation(k, v) * ImmutableRelation(k, v) -> ImmutableRelation(k, v)
imrel-subtract =
  ?(l, r); <imrel-subtract(|r)> l

external internal-immutable-relation-union :: (|ImmutableRelationImplBlob) ImmutableRelationImplBlob -> ImmutableRelationImplBlob

/**
 * Union with another immutable relation.
 * Returns a new relation with the union of the pairs in the two relation.
 *
 * @param other: ImmutableRelation - the relation to remove from the current term immutable relation
 * @type ImmutableRelation -> ImmutableRelation
 */
imrel-union(|ImmutableRelation(k, v)) :: ImmutableRelation(k, v) -> ImmutableRelation(k, v)
imrel-union(|r) = where(ImmutableRelation(other) := r);
  ImmutableRelation(internal-immutable-relation-union(|other))

/**
 * Union two immutable relations.
 * The given strategy merges the values of the intersecting keys.
 * Returns a new relation with the union.
 *
 * @param m: Term * Term -> Term
 * @type ImmutableRelation * ImmutableSet -> ImmutableRelation
 */
imrel-union :: ImmutableRelation(k, v) * ImmutableRelation(k, v) -> ImmutableRelation(k, v)
imrel-union =
  ?(l, r); <imrel-union(|r)> l
