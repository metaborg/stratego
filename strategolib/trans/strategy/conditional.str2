/**
 * Conditionals
 */
module strategy/conditional
imports strategy/general/option
imports collection/tuple/cons

strategies // Equality

  /**
   * Tests whether two terms are equal.
   */
  eq :: a * a -> a * a
  eq =
    ?(x, x)

  /**
   * Tests whether two terms are equal.
   */
  equal :: a * a -> a * a
  equal =
    ?(x, x)

  /**
   * Tests whether current term is equal to argument term.
   */

  equal(|?) :: a -> a
  equal(|x) =
    ?x

  /**
   * Tests whether two argument terms are equal to each other.
   */
  equal(|a, ?) :: b -> b
  equal(|x, y) =
    where(!x => y)

strategies // Control-flow combinators

  /**
   * try(s) tries to apply s to the current term, but
   * returns it unchanged (and succeeds) when s fails.
   */
  try(a -> a) :: a -> a
  try(s) =
    s <+ id

  if(a -> b, b -> a) :: a -> a
  if(c, b) =
    c < b + id

  if(a -> b, b -> c, a -> c) :: a -> c
  if(c, b1, b2) =
    c < b1 + b2

  /**
   * Apply restoring action 'rest' if s fails, and then fail.
   *
   * Typically useful if s performs side effects that should be
   * restored/undone in case s fails.
   */
  restore(a -> b, a -> ?) :: a -> b
  restore(s, rest) =
    s <+ (rest; fail)

  /**
   * Apply restoring action 'rest' after s terminates, and preserve
   * success/failure behaviour of s.
   *
   * Typically useful if s performs side effects that should be
   * restored always, e.g., when maintaining scope information.
   */
  restore-always(a -> a, a -> b) :: a -> b
  restore-always(s, rest) =
    s < rest + (rest; fail)

  /**
   * Applies s followed by f whether s failed or not.
   *
   * @result <s> input
   * @fail   s or f failed (consider try(f))
   * @type   a -> b, (a or b) -> _ :: a -> b
   */
  finally(a -> b, ? -> ?) :: a -> b
  finally(s, f) =
    s < where(f) + (where(f); fail)

strategies // Boolean combinators

  /**
   * true is a synonym for id.
   */
  true :: a -> a
  true =
    id

  /**
   * false is a synonym for fail.
   */
  false :: a -> b
  false =
    fail

  /**
   * ior(s1, s2) implements 'inclusive or', that is, the
   * inclusive choice of s1 and s2. It first tries s1, if
   * that fails it applies s2 (just like s1 <+ s2). However,
   * when s1 succeeds it also tries to apply s2.
   *
   * The results of the transformations are returned.
   */
  ior(a -> a, a -> a) :: a -> a
  ior(s1, s2) =
    (s1; try(s2)) <+ s2

  /**
   * or(s1, s2) is similar to ior(s1,s2), but the application
   * of the strategies is only tested.
   */
  or(a -> b, a -> b) :: a -> a
  or(s1, s2) =
    if s1 then try(where(s2)) else where(s2) end

  /**
   * and(s1, s2) applies s1 and s2 to the current
   * term and succeeds if both succeed. s2 will always
   * be applied, i.e., and is *not* a short-circuit
   * operator
   */
  and(a -> b, a -> b) :: a -> a
  and(s1, s2) =
    if s1 then where(s2) else where(s2); fail end

strategies

  maybe(a -> b) :: a -> Option(b)
  maybe(s) = !Some(<s>) <+ !None()

  maybe(a -> b, Tuple() -> b) :: Option(a) -> b
  maybe(s1, s2) : Some(a) -> <s1> a
  maybe(s1, s2) : None()  -> <s2> ()
