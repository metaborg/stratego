/**
 * This module contains strategies for checking local structural properties
 * on terms.
 *
 * @author Eelco Visser <visser@acm.org>
 * @author Karl Trygve Kalleberg <karltk@strategoxt.org> - documentation
 *
 */
module term/properties
imports
  strategy/traversal/simple
  strategy/conditional
  collection/list/common
  collection/list/lookup
  collection/list/cons
  collection/tuple/cons

strategies

  /**
   * Succeeds if the first argument is a subterm of the second.
   *
   * A term x is a subterm of a term y if x = y
   * or if x is a subterm of one of the chuldren of y.
   *
   * @type  (a, b) -> (a, b)
   */
  is-subterm :: a * b -> a * b
  is-subterm =
    ?(x, y); where(<oncetd(?x)> y)

  /**
   * Succeeds if the first argument (x) is a subterm of the second (y) and x is not y.
   *
   * @type  (a, b) -> (a, b)
   */
  is-proper-subterm :: a * b -> a * b
  is-proper-subterm =
    ?(x, y); not(is(? * ?);eq); is-subterm

strategies

  /**
   * Succeeds if the first argument (x) is a superterm of the second (y).
   *
   * @type  (a, b) -> (a, b)
   */
  is-superterm :: a * b -> a * b
  is-superterm =
    ?(x, y); where(<oncetd(?y)> x)

  /**
   * Succeeds if the first argument (x) is a superterm of the second (y) and x is not y.
   *
   * @type  (a, b) -> (a, b)
   */
  is-proper-superterm :: a * b -> a * b
  is-proper-superterm =
    ?(x, y); not(is(? * ?);eq); is-superterm

strategies

  is-proper-subterm-set :: List(a) * List(a) -> List(a) * List(a)
  is-proper-subterm-set =
    ?([y|_], xs); where(<getfirst(not(?y); oncetd(equal(|y :: ?)))> xs)

  is-proper-superterm-set :: List(a) * List(a) -> List(a) * List(a)
  is-proper-superterm-set =
    ?([x|_], ys); where(<getfirst(<is-proper-superterm>(x,<id>))> ys)

strategies


  /**
   * Succeeds if the current term has no direct subterms.
   *
   * @type  a -> a
   */
  is-leaf :: a -> a
  is-leaf =
    all(fail)

  /**
   * Succeeds if the current term has at least one direct subterm.
   *
   * @type  a -> a
   */
  is-inner-node :: a -> a
  is-inner-node =
    one(id)
