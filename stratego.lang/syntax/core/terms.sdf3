module core/terms

imports
  core/identifiers
  core/constants
  core/strategies

context-free sorts ID Var Wld
context-free syntax
  Var.Var = ID
  ID      = Id
  Wld.Wld = "_"

context-free sorts Term // term with annotations
context-free syntax

  Term      = Var
  Term      = Wld
  Term.Anno = <<PreTerm>{^<PreTerm>}>
  Term.As   = <<Var>@<Term>>
  Term.Path = PathTrm

context-free sorts PreTerm // term without annotations
context-free syntax

  PreTerm         = Var
  PreTerm         = Wld

  PreTerm.Int     = Int
  PreTerm.Real    = Real
  PreTerm.Str     = String
  PreTerm.Op      = <<Id>(<{Term ", "}*>)>
  PreTerm.OpQ     = <<String>(<{Term ", "}*>)>
  PreTerm.Explode = <<Term>#(<Term>)>
  PreTerm.AsPT    = <<Var>@<PreTerm>>

context-free priorities
  PreTerm.Explode > Term.As
//  , Var.Var > Term.Path
