module sugar/strategies
imports
  core/identifiers
  core/constants
  core/strategies
  core/terms
  sugar/terms
  sugar/rules
  sugar/constants
  sugar/signatures

template options
  tokenize: ")(|"

context-free sorts SDefP
context-free syntax
  StrategyDef.SDefNoArgs =
<<SId> =
  <Strategy>>

  StrategyDef.SDef =
<<SId>(<{Typedid ", "}*>) =
  <Strategy>>

  // Strategy definition with pattern matched term parameters
  StrategyDef = SDefP
  SDefP.SDefP =
<<SId>(<{Typedid ", "}*>|<{Term ","}+>) =
  <Strategy>>
  SDefP = SDefT {reject}

context-free syntax
  Typedid.DefaultVarDec = ID

context-free sorts Call With
context-free syntax

  Strategy = Call
  Call.Call = <<SVar>(<{Strategy ","}*>)>
  Call = With {reject}
  Strategy.ScopeDefault = <{<Strategy>}>
  Strategy.BA = [<[Strategy]> [Term]]
  Strategy.LChoice = [[Strategy] <+ [Strategy]] {right}

  Strategy.Rec        = <rec <SId>( <Strategy> )>
  Strategy.Not        = <not(<Strategy>)>
  Strategy.Where      = <where(<Strategy>)>
  Strategy = With
  With.With       = <with(<Strategy>)>
  Strategy.Test       = <test(<Strategy>)> {deprecated("Use `where` instead")}
  Strategy.PrimNoArgs = <prim(<String>)>
  Strategy.Prim       = <prim(<String>, <{Term ", "}*>)>

context-free syntax // congruence operators

  Strategy.StrCong        = <<String>>
  Strategy.IntCong        = <<Int>>
  Strategy.RealCong       = <<Real>>
  Strategy.CharCong       = <<Char>>
  Strategy.CongQ          = <<String>(<{Strategy ", "}*>)>
  Strategy.EmptyTupleCong = <( )>
  Strategy                = <(<Strategy>)> {bracket}
  Strategy.TupleCong      = <(<Strategy>, <{Strategy ", "}+> )>
  Strategy.ListCongNoTail = <[<{Strategy ", "}*> ]>
  Strategy.ListCong       = <[<{Strategy ", "}*> | <Strategy> ]>
  Strategy.ExplodeCong    = <<Strategy>#(<Strategy>)>
  Strategy.AM             = [[Strategy] => [Term]]
  Strategy.Assign         = <<Term> := <Term>>

  Strategy.AnnoCong       = <<Strategy>{<Strategy>}>

// Syntactic sugar sugar

context-free syntax

  Strategy.CallNoArgs = <<SVar>>
  Strategy.LRule      = <\\<Rule>\\>
  Strategy.SRule      = <(<Rule>)>
  Strategy.Choice     = <<Strategy> + <Strategy>> {right}
  Strategy.RChoice    = [[Strategy] +> [Strategy]] {right, deprecated("Flip your arguments and use <+")}
  Strategy.Proceed    = <proceed(<{Strategy ", "}+>)>
  Strategy.CondChoice = <if <Strategy> then <Strategy> else <Strategy> end>
  Strategy.IfThen     = <if <Strategy> then <Strategy> end>
  Strategy.LTrySome   = [[Strategy] <* [Strategy]] {left}

context-free sorts SwitchCase
context-free syntax
  Strategy.SwitchChoiceNoOtherwise =
<switch <Strategy>
  <{SwitchCase "\n"}*>
end>

  Strategy.SwitchChoice =
<switch <Strategy>
  <{SwitchCase "\n"}*>
  otherwise : <Strategy>
end>

  SwitchCase.SwitchCase =
<case <Strategy>:
  <Strategy>>

context-free priorities
  { Strategy.AnnoCong Strategy.ExplodeCong }
> { Strategy.Match Strategy.Build }
> Strategy.BA
> Strategy.AM
> Strategy.Seq
> Strategy.LTrySome
> {right:
   Strategy.Choice
   Strategy.LChoice
   Strategy.RChoice
   Strategy.GuardedLChoice
  }

// Strategy.AnnoCong and Strategy.ExplodeCong are same syntax as Term.AnnoList and Term.Explode
//   respectively. So we need to disambiguate that we prefer the Term version to be on the
//   right-hand side of Strategy productions that have a Term as the right-most part of their rule,
//   and this is expressed by that we don't want such a production inside AnnoCong or ExplodeCong as
//   the left-hand child.
context-free priorities
  Strategy.AnnoCong <0>
  .> { Strategy.Match Strategy.Build Strategy.Assign Strategy.BA Strategy.AM },

  Strategy.ExplodeCong <0>
  .> { Strategy.Match Strategy.Build Strategy.Assign Strategy.BA Strategy.AM }

// Normal priorities work on operator priority semantics using the outer recursive positions. 
//   Strategy.GuardedLChoice and Strategy.Choice have a larger overlap, therefore we need to
//   disambiguate that we want `a < b + (c + d)` not `a < (b + c) + d` when there are no
//   parentheses. Therefore we disallow Strategy.Choice as the second argument to
//   Stratego.GuardedLChoice. This _should_ be under index <1>, but works as we want it under <2>,
//   which is probably a bug in SDF3 or JSGLR2?
context-free priorities
  Strategy.GuardedLChoice <2>
  .> Strategy.Choice
