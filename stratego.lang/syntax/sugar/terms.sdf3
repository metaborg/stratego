module sugar/terms
imports
  core/terms
  core/identifiers
  sugar/constants
  sugar/strategies

context-free syntax
  ID.ListVar        = LId

context-free syntax

  Term.BuildDefault = <_<Term>>
  Term.AnnoList     = <<PreTerm>{<{Term ", "}*>}>
  Term.NoAnnoList   = PreTerm
  Term.App          = [<[Strategy]> [Term]]
  Term.RootApp      = [<[Strategy]>]

context-free syntax

  PreTerm.Char      = Char
  PreTerm.AppPT     = [<[Strategy]> [Term]]
  PreTerm.RootAppPT = [<[Strategy]>]
  PreTerm.Tuple     = <(<{Term ", "}*>)>
  PreTerm.List      = <[<{Term ", "}*>]>
  PreTerm.ListTail  = <[<{Term ", "}+> | <Term>]>

  PreTerm.BuildDefaultPT = <_<PreTerm>>

context-free priorities
// replacement for prefer on Term duplicates of PreTerm rules
  Term.NoAnnoList = PreTerm <0> .> {
    PreTerm = Var
    PreTerm = Wld
  }
, Term.NoAnnoList <0> .> {
    PreTerm.AsPT
    PreTerm.AppPT
    PreTerm.RootAppPT
    PreTerm.BuildDefaultPT
  }
// AnnoList binds tighter
, Term.AnnoList > {
    PreTerm.AsPT
    PreTerm.AppPT
  }
, Term.Anno > PreTerm.AppPT
, PreTerm.Explode > { Term.App PreTerm.AppPT Term.BuildDefault PreTerm.BuildDefaultPT }