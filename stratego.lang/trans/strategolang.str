module strategolang

imports

  completion/completion
  pp
  outline
  analysis
  signatures/core/modules-sig
  signatures/-

  strc
  strc/front/frontend
  strc/incr/compile

  strj
  strj/s2j
  strj/strj-options

  libjava-front
  libstratego-gpp
  libstratego-lib

rules
  translate-core(|path, O) = id
    ; where(<set-config> ("--library", ())) // no main strategy necessary this way
    ; local-pre-desugar-all
    ; try(\ Module(_, d*) -> Specification(<remove-all(?Imports(_))> d*) \)
    ; where(
        <set-config> ("-o", <guarantee-extension(|$[opt[O].java])> path) // required output file path
      ; strj-preinit-options
      ; <set-config> ("-O", O)
      ; <dirname> path
      ; rules(OutputDirName := <id>)
      )
    ; frontend
    ; strc-optimize

rules // Debugging

  // Show parsed AST
  debug-show-aterm:
    (node, _, _, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"aterm")> path
    ; result   := node

  debug-show-desugared:
    (node, _, _, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"ctree.aterm")> path
    ; <set-config> ("--library", ()) // no main strategy necessary this way
    ; !node
    ; local-pre-desugar-all
    ; \Module(_, d*) -> Specification(<remove-all(?Imports(_))> d*)\
    ; frontend-sep-comp
    ; ?result

  // Show desugared AST
  debug-show-ctree:
    (node, _, _, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"ctree.aterm")> path
    ; <translate-core(|path, 2)> node => result

  // Show desugared concrete syntax
  debug-show-core:
    (node, _, _, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"core.str2")> path
    ; <translate-core(|path, 2); pp-stratego-core> node => result

  debug-show-java:
    (node, _, _, path, project-path) -> (filename*, result*)
    with
      <set-config> ("--library", ()) // no main strategy necessary this way
    ; !node
    ; local-pre-desugar-all
    ; \Module(_, d*) -> Specification(<remove-all(?Imports(_))> d*)\
    ; frontend-sep-comp
    ; Specification(map(try(Strategies(remove-all(?ExtSDef(_, _, _))))))
    ; debug1
    ; where(
        <set-config> ("-o", <guarantee-extension(|"java")> path) // required output file path
      ; <set-config> ("--single-strategy", ())
      ; <set-config> ("--fusion", "off")
      ; <set-config> ("--cache-dir", ".")
      ; <set-config> ("-p", "test")
      ; strj-preinit-options
      ; <dirname> path
      ; rules(OutputDirName := <id>)
      )
    ; s2j
    ; ?ast*
    ; filename* := <map(output-filename)> ast*
    ; result* := <map(pp-java5-to-abox; box2text-string(|80) <+ where(pp-aterm; debug(|"Pretty printing Java failed: ")); fail)> ast*

  debug-show-java-O0 = debug-show-java(|0)
  debug-show-java-O1 = debug-show-java(|1)
  debug-show-java-O2 = debug-show-java(|2)
  debug-show-java-O3 = debug-show-java(|3)
  debug-show-java-O4 = debug-show-java(|4)
  debug-show-java-O4-unopt-DFA = where(<set-config> ("--no-dfa-optimise", ())); debug-show-java(|4)
  debug-show-java-O5 = debug-show-java(|5)
  debug-show-java-O6 = debug-show-java(|6)

  debug-show-java(|O):
    (node, _, _, path, project-path) -> (filename*, result*)
    with
      <translate-core(|path, O)> node
    ; s2j
    ; ?ast*
    ; filename* := <map(output-filename)> ast*
    ; result* := <map(pp-java5-to-abox; box2text-string(|80))> ast*

imports
  strc/opt/bound-unbound-vars

strategies

  debug-show-core-optimized-O1 = debug-show-core-optimized(|1)
  debug-show-core-optimized-O2 = debug-show-core-optimized(|2)
  debug-show-core-optimized-O3 = debug-show-core-optimized(|3)
  debug-show-core-optimized-O4 = debug-show-core-optimized(|4)
  debug-show-core-optimized-O4-analysed = debug-show-core-optimized(mark-bound-unbound-vars|4)
  debug-show-core-optimized-O4-backend-simplify = debug-show-core-optimized(s2j-init-options; backend-simplify|4)
  debug-show-core-optimized-O5 = debug-show-core-optimized(|5)
  debug-show-core-optimized-analysed-O5 = debug-show-core-optimized(mark-bound-unbound-vars|5)
  debug-show-core-optimized-O5-unopt-DFA = where(<set-config> ("--no-dfa-optimise", ())); debug-show-core-optimized(|5)
  debug-show-core-optimized-O6 = debug-show-core-optimized(|6)

  debug-show-ctree-optimized-O1 = debug-show-ctree-optimized(|1)
  debug-show-ctree-optimized-O2 = debug-show-ctree-optimized(|2)
  debug-show-ctree-optimized-O3 = debug-show-ctree-optimized(|3)
  debug-show-ctree-optimized-O4 = debug-show-ctree-optimized(|4)
  debug-show-ctree-optimized-O4-analysed = debug-show-ctree-optimized(mark-bound-unbound-vars|4)
  debug-show-ctree-optimized-O4-backend-simplify = debug-show-ctree-optimized(s2j-init-options; backend-simplify|4)
  debug-show-ctree-optimized-O5 = debug-show-ctree-optimized(|5)
  debug-show-ctree-optimized-O5-analysed = debug-show-ctree-optimized(mark-bound-unbound-vars|5)
  debug-show-ctree-optimized-O6 = debug-show-ctree-optimized(|6)

  debug-show-ctree-optimized(|O) = debug-show-ctree-optimized(id|O)
  debug-show-ctree-optimized(s|O):
    (node, _, _, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|$[core.opt[O].aterm])> path
    ; <translate-core(|path, O); s> node => result

  debug-show-core-optimized(|O) = debug-show-core-optimized(id|O)
  debug-show-core-optimized(s|O):
    (node, _, _, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|$[core.opt[O].str2])> path
    ; <translate-core(|path, O); s; pp-stratego-core> node => result

// Testing strategies
imports
  strc/front/desugar
  strc/opt/multimatch/multimatch2dfa

strategies // for testing pruposes only
  test-choice2multimatch = test-menuitem-aterm(|4)
  test-choice2multimatch-pp = test-menuitem-pp(|4)

  test-multimatch2dfa = test-menuitem-aterm(id, topdown(try(multimatch2dfa)))
  test-multimatch2dfa-pp = test-menuitem-pp(id, topdown(try(multimatch2dfa)))

  test-menuitem-pp(|O) = test-menuitem-pp(id, id|O)
  test-menuitem-pp(s1, s2) = test-menuitem-pp(s1, s2|2)
  test-menuitem-pp(s1, s2|O) = test-menuitem(s1, s2, pp-stratego-core|O, "str2")

  test-menuitem-aterm(|O) = test-menuitem-aterm(id, id|O)
  test-menuitem-aterm(s1, s2) = test-menuitem-aterm(s1, s2|2)
  test-menuitem-aterm(s1, s2|O) = test-menuitem(s1, s2, id|O, "aterm")

  test-menuitem(s1, s2|O, ext) = test-menuitem(s1, s2, id|O, ext)
  test-menuitem(s1, s2, pp|O, ext):
    (node, _, _, path, project-path) -> (filename, result)
  with
    filename := <guarantee-extension(|$[test.[ext]])> path
  ; <test-translate-core(s1, s2|path, O); pp> node => result

  test-translate-core(|path, O) = test-translate-core(id, id|path, O)
  test-translate-core(s1, s2|path, O) = id
  ; where(
      <set-config> ("--library", ()) // no main strategy necessary this way
    ; <set-config> ("-O", O)
    )
  ; local-pre-desugar-all
  ; try(\ Module(_, d*) -> Specification(<remove-all(?Imports(_))> d*) \)
  ; s1
  ; strc-optimize
  ; s2
