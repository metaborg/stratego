module gradual-types/environment

imports

libstratego-lib

gradual-types/utils
gradual-types/signatures
signatures/-
signatures/gradual-types/internal-sig
signatures/gradual-types/signatures-sig
signatures/core/signatures-sig
 
libspoofax/stratego/debug

signature
sorts

Environment
Store

constructors

//    strategies                        constructors                 sorts
Env : Map/*(str * int * int, SType)*/ * Rel/*(str * int, OpType)*/ * Set/*(str * int)*/ -> Environment
//   local variables     currentTermType type variables
St : Map/*(str, Type)*/ * Type         * Map/*(str, Type)*/ -> Store
//                    outputTermType strategiesAfterMatch
StMatchTerm : Store * Option(Type) * List(Strategy) -> Store
//                    inputTermType // currentTermType in Store is the _expected_ current term type
StBuildTerm : Store * Type -> Store

strategies

new-env: (strats, constrs, srts) -> Env(strats, constrs, srts)
new-st = !St(<immap-new>, DynT(), <immap-new>)
stMatchTerm-from-st: st -> StMatchTerm(<current-type(TP2tv)> st, None(), [])
stBuildTerm-from-st: st -> StBuildTerm(<current-type(|DynT())> st, <current-type> st)
st-from-stMatchTerm: StMatchTerm(st, Some(t), s*) -> (<current-type(|<tv2TP> t)> st, s*)
st-from-stMatchTerm: StMatchTerm(st, None(), s*) -> (<current-type(tv2TP)> st, s*)
st-from-stBuildTerm: StBuildTerm(st, _) -> <current-type(tv2TP)> st

stMatchTerm-as-st(s|msg): StMatchTerm(st1, ot1, s*) -> StMatchTerm(<current-type-of(|st1)> st2, ot2, s*)
  where
    st2 := <current-type(tv2TP);s> st1
  ; if None() := ot1
      then ot2 := Some(<current-type> st2)
      else ot2 := ot1; <report-Warn> msg
    end
stBuildTerm-as-st(s): StBuildTerm(st1, t1) -> StBuildTerm(st2, t1)
  where
    // note we're using the inputtype here, not the current type
    st2 := <current-type(|t1);s> st1

strat-env = ?Env(<id>, _, _)
strat-env(s) = Env(s, id, id)

// Note that this also tries to find a congruence if it fails to find a strategy
strat-lookup(|name, snum, tnum) =
  strat-env
; (  immap-get(|(name, snum, tnum))
  <+ immap-get(|(name, 0, 0)); ?SDyn()
  )

congr-lookup(|name, snum, tnum, type) =
    where(0 := tnum)
  ; constr-env
  ; imrel-get(|(name, snum))
  ; imset-to-list
  ; not(?[])
  ; if DynT() := type <+ IllFormedTermT(_, _) := type <+ EmptyT() := type
    then
      \[ConstrType(args, sort)] -> FunTType(<map(!FunTType(DynT(<id>), DynT(<id>)))> args, [], DynT(sort), DynT(sort))\
      <+ !FunTType(<range;map(!SDyn())> snum, [], DynT(Dyn()), DynT(Dyn()))
    else
      \[ConstrType(args, sort)] -> FunTType(<map(!FunTType(<id>, <id>))> args, [], sort, sort)\
      <+ !FunTType(<range;map(!SDyn())> snum, [], DynT(Dyn()), DynT(Dyn()))
    end

strat-put(|name, snum, tnum, value) = strat-env(immap-put(|(name, snum, tnum), value))
strat-merge-env(s): (e, se) -> <strat-env(immap-union(s|se))> e
strat-copy-type(|name, snum, tnum, as) = strat-env(env-copy-type(|(name, snum, tnum), (as, snum, tnum)))
strat-zip-new-env: (names, types) -> <zip-new-env> (names-arities, types)
where if <map(is-string)> names
  then names-arities := <zip(name-type-to-arity)> (names, types)
  else names-arities := names
end

name-type-to-arity: (n, FunTType(st*, tt*, _){LocalDef()}) -> (n, <length> st*, <length> tt*)
name-type-to-arity: (n, SDyn(){LocalDef()}) -> (n, 0, 0)

constr-env = ?Env(_, <id>, _)
constr-env(s) = Env(id, s, id)

// N.B. Use constr-lookup/3 from main.str to filter on sort of the constructor.
constr-lookup(|name, arity) = constr-env; imrel-get(|(name, arity)); imset-to-list
constr-put(|name, arity, value) = constr-env(imrel-insert(|(name, arity), value))
constr-merge-env: (e, ce) -> <constr-env(imrel-union(|ce))> e

overlay-lookup(|name, arity) = constr-env; imrel-get(|(name, arity)); imset-to-list; filter(?_{Overlay()})

sort-env = ?Env(_, _, <id>)
sort-env(s) = Env(id, id, s)

sort-exists(|name, arity) = sort-env; imset-contains(|(name, arity))
sort-add(|name, arity) = sort-env(imset-insert(|(name, arity)))
sort-merge-env: (e, se) -> <sort-env(imset-union(|se))> e

type-var-lookup(|name) = type-var-env; ?env; type-var-lookup(|name, env)
type-var-lookup(|name, env) = <immap-get(|name)> env; try(recurse-type-var-lookup(|name, env)); not(oncetd(?SortVar(name)))
type-var-put(|name, type) = type-var-env(immap-put(|name, type))
type-var-clear(|names) = type-var-env(\env -> <foldl(\(name, e) -> <immap-remove(|name)> e\)> (names, env)\)

type-var-elim(|subj, names) = type-var-elim(id|names)
type-var-elim(lookupfail|name) = where(<is-string> name); type-var-elim(lookupfail, ?name <+ where(!<id>{};?name))
type-var-elim(lookupfail|names) = where(<is-list> names); type-var-elim(lookupfail, \name -> <getfirst(?name <+ where(!<id>{};?name))> names\)

type-var-elim(lookupfail, match) =
    where(env := <type-var-env>)
  ; type-var-elim-(
      {name,type: (SortVar(name) -> type
          where
            <match> name
          ; type := <type-var-lookup(|name, env) <+ <lookupfail> name;!EmptyT()>)}
    , match
    )

type-var-elim-(s, match) = St(immap-map-values(alltd(s)), alltd(s), immap-map-values(alltd(s));immap-filter-keys(not(match)))
type-var-elim-(s, match) = StMatchTerm(type-var-elim-(s, match), alltd(s), id)
type-var-elim-(s, match) = StBuildTerm(type-var-elim-(s, match), alltd(s))

recurse-type-var-lookup(|name1, env): SortVar(name2) -> <immap-get(|name2); try(recurse-type-var-lookup(|name2, env))> env
where <not(eq)> (name1, name2)

term-env = ?St(<id>, _, _)
term-env = ?StMatchTerm(<term-env>, _, _)
term-env = ?StBuildTerm(<term-env>, _)
term-env(s) = St(s, id, id)
term-env(s) = StMatchTerm(term-env(s), id, id)
term-env(s) = StBuildTerm(term-env(s), id)

term-lookup(|name) = term-env; immap-get(|name)
term-put(|name, value) = term-env(immap-put(|name, value))
term-merge-env(s): (e, te) -> <term-env(immap-union(s|te))> e
term-zip-new-env = zip-new-env

current-type = ?St(_, <id>, _)
current-type = ?StMatchTerm(<current-type>, _, _)
current-type = ?StBuildTerm(<current-type>, _)
current-type(|t) = St(id, !t, id)
current-type(|t) = StMatchTerm(current-type(|t), id, id)
current-type(|t) = StBuildTerm(current-type(|t), id)
current-type(s) = St(id, s, id)
current-type(s) = StMatchTerm(current-type(s), id, id)
current-type(s) = StBuildTerm(current-type(s), id)
current-type-of(|st) = current-type(|<current-type> st)

// alias of current-type for StBuildTerm, this more accurately describes the role of current-type
// when passing down a type during type-checking of a BuildTerm
expected-type = ?StBuildTerm(<current-type>, _)
expected-type(|t) = StBuildTerm(current-type(|t), id)
expected-type(s) = StBuildTerm(current-type(s), id)
expected-type-of(|st) = expected-type(|<expected-type> st)

type-var-env = ?St(_, _, <id>)
type-var-env = ?StMatchTerm(<type-var-env>, _, _)
type-var-env = ?StBuildTerm(<type-var-env>, _)
type-var-env(s) = St(id, id, s)
type-var-env(s) = StMatchTerm(type-var-env(s), id, id)
type-var-env(s) = StBuildTerm(type-var-env(s), id)

output-type(|t) = StMatchTerm(id, \None() -> Some(t)\, id)
after-match(|s) = StMatchTerm(id, id, ![s | <id>])
after-match = ?StMatchTerm(_, _, <id>)

strategies

/**
 * Copy type of `name` and bind it to the new name (`as`) as well. 
 */
env-copy-type(|name, as): env -> <immap-put(|as, <immap-get(|name)> env)> env

zip-new-env = zip;immap-from-list

keep-left = Fst
keep-right = Snd
