module gradual-types/defs

imports

libstratego-lib

libspoofax/term/annotation
libspoofax/stratego/debug

signatures/sugar/modules-sig
signatures/sugar/rules-sig
signatures/sugar/strategies-sig
signatures/core/strategies-sig
signatures/gradual-types/modules-sig
signatures/gradual-types/signatures-sig
signatures/gradual-types/strategies-sig
signatures/gradual-types/internal-sig

gradual-types/environment
gradual-types/main
gradual-types/signatures
gradual-types/strategies
gradual-types/terms
gradual-types/utils

strategies // Def

insertCasts-Def(|e1):
  (d, st1) -> <insertCasts-Def(|e1, st1)> d

insertCasts-Def(|e1, st1) = otf12(insertCasts-Def-(|e1, st1))

// insertCasts-Def(|Environment, Store) :: Def -> Def
insertCasts-Def-(|e1, st1):
  AnnoDef(annos, sr) -> (AnnoDef(annos, s'), st2)
  with
    (n, sn, tn) := <nameOf-StrategyDef-RuleDef> sr
  ; if <fetch(?Extend() + ?Override())> annos
      // Leaving this in the env for ProceedT/2 and ProceedNoArgs/0 handling
      then e2 := <strat-copy-type(|n, sn, tn, ProceedNoArgs())> e1
      else // TODO: check overlap between internal and any other definitions
        e2 := e1
    end
  ; (s', st2) := <insertCasts-Def(|e2, st1)> sr

insertCasts-Def(|e1, st1) = insertCasts-StrategyDef(|e1, st1)

insertCasts-Def(|e1, st1) = insertCasts-RuleDef(|e1, st1)
insertCasts-Def-(|e1, st1):
  d -> (<desugar-DefHasType;insertCasts-DefHasType-(|e1)> d, st1)

insertCasts-DefHasType-(|e1): DefHasType(_, funttype) -> <id>
  with <sort-check-FunTType(|e1)> funttype

strategies

last-seq(s) = otf(Seq(id, last-seq(s))) <+ s

prepare-strategy-def(|st1):
  t1@FunTType(_, _, _) -> (t2, st2)
  where
    t2 := <alltd(\tv@SortVar(x) -> Sort(x{tv}, [])\)> t1
  ; tvs := <collect(\SortVar(x) -> x where not(<type-var-lookup(|x)> st1)\)> t1
  ; st2 := <foldl(\(tv, st) -> <type-var-put(|tv, Sort(tv{SortVar(tv)}, []))> st\)> (tvs, st1)

prepare-strategy-def(|st1):
  t1{LocalDef()} -> (t2, st2)
  where
    t2 := <alltd(\SortVar(x) -> <type-var-lookup(|x)> st1\ <+ otf(\tv@SortVar(x) -> Sort(x{tv}, [])\))> t1
  ; tvs := <collect(\SortVar(x) -> x where not(<type-var-lookup(|x)> st1)\)> t1
  ; st2 := <foldl(\(tv, st) -> <type-var-put(|tv, Sort(tv{SortVar(tv)}, []))> st\)> (tvs, st1)

strat-lookup-def(|x, sn, tn, st1) =
    ?env
  ; ( strat-lookup(|x, sn, tn)
    < where(try(
        <congr-lookup(|x, sn, tn, DynT())> env
      ; <report-Err> (x, StrategyCongruenceOverlap())
      ))
    + congr-lookup(|x, sn, tn, DynT())
    )
  ; prepare-strategy-def(|st1)

strategies // StrategyDef

insertCasts-StrategyDef(|e1, st1) = otf12(insertCasts-StrategyDef-(|e1, st1))

// insertCasts-StrategyDef(|Environment, Store) :: Decl -> (Decl, Store)
insertCasts-StrategyDef-(|e1, st1):
  d@SDefT(x, s1*, t1*, s1) -> (SDefT(x, s1*, t1*, s3), st5)
  where
    (FunTType(st*, tt*, TP()), st2) := <strat-lookup-def(|x{}, <length> s1*, <length> t1*, st1)> e1
  with
    tv1 := <local-newname> "tp"
  ; tv2 := "def"
  ; t1 := Sort(TP(), []){tv1}
  ; s2* := <map(?DefaultVarDec(<id>) <+ ?VarDec(<id>, _))> s1*
  ; t2* := <map(?DefaultVarDec(<id>) <+ ?VarDec(<id>, _))> t1*
  ; e2 := <strat-merge-env(keep-right)> (e1, <strat-zip-new-env> (s2*, <map(\st -> st{LocalDef()}\)> st*))
  ; st3 := <term-merge-env(keep-right);current-type(|t1)> (st2, <term-zip-new-env> (t2*, tt*))
  ; (s2, st4) := <insertCasts-Strategy(|e2, st3)> s1
  ; t2 := <current-type> st4
  ; if <itv(|st4);eq> (t1, t2)
      <+ <eq> (t2, <type-var-lookup(|tv1)> st4)
         ; <not(oncetd(?DynT()))> t2
      <+ <?EmptyT()> t2
      then id
      else <report-Err> (x, MatchNotSpecificEnoughForTP(t2))
    end
  ; if <eq> (t2, <type-var-lookup(|tv1)> st4)
      then s3 := Seq(TypeTest(<itv(|st4)> t2), s2)
      else s3 := s2
    end
  ; st5 := <type-var-elim(id|[tv1, tv2])> st4

insertCasts-StrategyDef-(|e1, st1):
  d@SDefT(x, s1*, t1*, s1) -> (SDefT(x, s1*, t1*, s3), st6)
  where
    (FunTType(st*, tt*, t1, t2), st2) := <strat-lookup-def(|x{}, <length> s1*, <length> t1*, st1)> e1
  with
    tv := "def"
  ; s2* := <map(?DefaultVarDec(<id>) <+ ?VarDec(<id>, _))> s1*
  ; t2* := <map(?DefaultVarDec(<id>) <+ ?VarDec(<id>, _))> t1*
  ; e2 := <strat-merge-env(keep-right)> (e1, <strat-zip-new-env> (s2*, <map(\st -> st{LocalDef()}\)> st*))
  ; st3 := <term-merge-env(keep-right);current-type(|t1)> (st2, <term-zip-new-env> (t2*, tt*))
  ; (s2, st4) := <insertCasts-Strategy(|e2, st3)> s1
  ; s3 := <last-seq(\s -> <insertCastAfter(|<current-type> st4, t2);?(<id>, st5)> (s, st4)\)> s2
  ; st6 := <type-var-elim(id|tv)> st5

insertCasts-StrategyDef-(|e1, st1):
  d@SDefP(x, s1*, t1*, s1) -> (SDefP(x, s1*, t1*, s3), st5)
  where
    (FunTType(st*, tt*, TP()), st2) := <strat-lookup-def(|x{}, <length> s1*, <length> t1*, st1)> e1
  with
    tv1 := <local-newname> "tp"
  ; tv2 := "def"
  ; t1 := Sort(TP(), []){tv1}
  ; s2* := <map(?DefaultVarDec(<id>) <+ ?VarDec(<id>, _))> s1*
  ; e2 := <strat-merge-env(keep-right)> (e1, <strat-zip-new-env> (s2*, <map(\st -> st{LocalDef()}\)> st*))
  ; (t2*, (st3, s*)) := <thread-map-otf(insertCasts-MatchTerm(|e2))> (<zip> (t1*, tt*), (st2, []))
  ; (s2, st4) := <insertCasts-Strategy(|e2, st3)> s1
  ; t2 := <current-type> st4
  ; if <eq> (t1, t2)
      <+ <eq> (t2, <type-var-lookup(|tv1)> st4)
         ; <not(oncetd(?DynT()))> t2
      <+ <?EmptyT()> t2
      then id
      else <report-Err> (x, MatchNotSpecificEnoughForTP(t2))
    end
  ; if <eq> (t2, <type-var-lookup(|tv1)> st4)
      then s'* := [ TypeTest(<itv(|st4)> t2), s* ]
      else s'* := s*
    end
  ; s3 := <StrategyDef-after-match> (s'*, s2)
  ; st5 := <type-var-elim(id|[tv1, tv2])> st4

insertCasts-StrategyDef-(|e1, st1):
  d@SDefP(x, s1*, t1*, s1) -> (SDefP(x, s1*, t1*, s4), st6)
  where
    (FunTType(st*, tt*, t1, t2), st2) := <strat-lookup-def(|x{}, <length> s1*, <length> t1*, st1)> e1
  with
    tv := "def"
  ; s2* := <map(?DefaultVarDec(<id>) <+ ?VarDec(<id>, _))> s1*
  ; e2 := <strat-merge-env(keep-right)> (e1, <strat-zip-new-env> (s2*, <map(\st -> st{LocalDef()}\)> st*))
  ; (t2*, (st3, s*)) := <thread-map-otf(insertCasts-MatchTerm(|e2))> (<zip> (t1*, tt*), (st2, []))
  ; (s2, st4) := <insertCasts-Strategy(|e2, st3)> s1
  ; s3 := <last-seq(\s -> <insertCastAfter(|<current-type> st4, t2);?(<id>, st5)> (s, st4)\)> s2
  ; s4 := <StrategyDef-after-match> (s*, s3)
  ; st6 := <type-var-elim(id|tv)> st5

StrategyDef-after-match: ([], s) -> s
StrategyDef-after-match: (s*, s) -> <otf(!Where(<seqs> s*));otf(!Seq(<id>, s))> s

insertCasts-StrategyDef(|e1, st1) =
  otf(\SDef(x, s1*, s1) -> SDefT(x, s1*, [], s1)\)
; insertCasts-StrategyDef(|e1, st1)
; (otf(\SDefT(x, s1*, [], s1) -> SDef(x, s1*, s1)\), id)

insertCasts-StrategyDef(|e1, st1) =
  otf(\SDefNoArgs(x, s1) -> SDefT(x, [], [], s1)\)
; insertCasts-StrategyDef(|e1, st1)
; (otf(\SDefT(x, [], [], s1) -> SDefNoArgs(x, s1)\), id)

insertCasts-StrategyDef-(|e1, st1) =
    \ExtSDefInl(name, sargs, targs, body) -> SDefT(name, sargs, targs, body)\
  ; insertCasts-StrategyDef(|e1, st1)
  ; (\SDefT(name, sargs, targs, body) -> ExtSDefInl(name, sargs, targs, body)\, id)

insertCasts-StrategyDef-(|e1, st1) =
    {name, sargs, targs, body: (ExtTypedDefInl(name, sargs, targs, t, body) -> SDefT(name, sargs, targs, body))}
  ; insertCasts-StrategyDef(|e1, st1)
  ; with(<sort-check-FunTType(|e1)> t)
  ; (\SDefT(name, sargs, targs, body) -> ExtTypedDefInl(name, sargs, targs, t, body)\, id)

insertCasts-StrategyDef-(|e1, st1):
  d@ExtSDef(_, _, _) -> (d, st1)

insertCasts-StrategyDef-(|e1, st1):
  d@ExtTypedDef(_, funttype) -> (d, st1)
  with <sort-check-FunTType(|e1)> funttype

strategies // RuleDef

insertCasts-RuleDef(|e1, st1) = otf12(insertCasts-RuleDef-(|e1, st1))

insertCasts-RuleDef(|e1, st1) =
  otf(\RDefNoArgs(x, r) -> RDefT(x, [], [], r)\)
; insertCasts-RuleDef(|e1, st1)
; (otf(\RDefT(x, [], [], r) -> RDefNoArgs(x, r)\), id)

insertCasts-RuleDef(|e1, st1) =
  otf(\RDef(x, s1*, s1) -> RDefT(x, s1*, [], s1)\)
; insertCasts-RuleDef(|e1, st1)
; (otf(\RDefT(x, s1*, [], s1) -> RDef(x, s1*, s1)\), id)

insertCasts-RuleDef-(|e1, st1):
  d@RDefT(x, s1*, t1*, r1@Rule(trm1, trm2, cs1)) -> (RDefT(x, s1*, t1*, r2), st7)
  where
    (FunTType(st*, tt*, TP()), st2) := <strat-lookup-def(|x{}, <length> s1*, <length> t1*, st1)> e1
  with
    s2* := <map(?DefaultVarDec(<id>) <+ ?VarDec(<id>, _))> s1*
  ; t2* := <map(?DefaultVarDec(<id>) <+ ?VarDec(<id>, _))> t1*
  ; e2 := <strat-merge-env(keep-right)> (e1, <strat-zip-new-env> (s2*, <map(\st -> st{LocalDef()}\)> st*))
  ; st3 := <term-merge-env(keep-right)> (st2, <term-zip-new-env> (t2*, tt*))
  ; tv1 := <local-newname> "tp"
  ; tv2 := "def"
  ; t1 := SortVar(tv1){TP()}
  ; (trm3, (st4, s*)) := <insertCasts-MatchTerm(|e2, <current-type(|t1)> st3)> trm1
  ; if <type-var-lookup(|tv1);not(?SortVar(_) + oncetd(?DynT()))> st4
      then id
      else <report-Err> (trm1, MatchNotSpecificEnoughForTP(<current-type> st3))
    end
  ; t3 := <current-type> st4
  ; (cs2, st5)  := <thread-map-otf(insertCasts-RuleCond(|e2))> (cs1, st4)
  ; cs3 := <RuleDef-after-match> (s*, cs2)
  ; (trm4, st6) := <insertCasts-BuildTerm(|e2, st5, t3)> trm2
  ; st7 := <type-var-elim(id|[tv1, tv2])> st6
  ; r2 := <otf(!Rule(trm3, trm4, cs3))> r1

insertCasts-RuleDef-(|e1, st1):
  d@RDefT(x, s1*, t1*, r1@Rule(trm1, trm2, cs1)) -> (RDefT(x, s1*, t1*, r2), <current-type-of(|st5)> st7)
  where
    (FunTType(st*, tt*, t1, t2), st2) := <strat-lookup-def(|x{}, <length> s1*, <length> t1*, st1)> e1
  with
    s2* := <map(?DefaultVarDec(<id>) <+ ?VarDec(<id>, _))> s1*
  ; t2* := <map(?DefaultVarDec(<id>) <+ ?VarDec(<id>, _))> t1*
  ; e2 := <strat-merge-env(keep-right)> (e1, <strat-zip-new-env> (s2*, <map(\st -> st{LocalDef()}\)> st*))
  ; st3 := <term-merge-env(keep-right)> (st2, <term-zip-new-env> (t2*, tt*))
  ; tv := "def"
  ; (trm3, (st4, s*)) := <insertCasts-MatchTerm(|e2, <current-type(|t1)> st3)> trm1
  ; t3 := <current-type> st4
  ; (cs2, st5)  := <thread-map-otf(insertCasts-RuleCond(|e2))> (cs1, st4)
  ; cs3 := <RuleDef-after-match> (s*, cs2)
  ; (trm4, st6) := <insertCasts-BuildTerm(|e2, st5, t2)> trm2
  ; st7 := <type-var-elim(id|tv)> st6
  ; r2 := <otf(!Rule(trm3, trm4, cs3))> r1

insertCasts-RuleDef-(|e1, st1):
  d@RDefP(x, s1*, t1*, r1@Rule(trm1, trm2, cs1)) -> (RDefP(x, s1*, t2*, r2), st7)
  where
    (FunTType(st*, tt*, TP()), st2) := <strat-lookup-def(|x{}, <length> s1*, <length> t1*, st1)> e1
  with
    s2* := <map(?DefaultVarDec(<id>) <+ ?VarDec(<id>, _))> s1*
  ; e2 := <strat-merge-env(keep-right)> (e1, <strat-zip-new-env> (s2*, <map(\st -> st{LocalDef()}\)> st*))
  ; (t2*, (st3, s1*)) := <thread-map-otf(insertCasts-MatchTerm(|e2))> (<zip> (t1*, tt*), (st2, []))
  ; tv1 := <local-newname> "tp"
  ; tv2 := "def"
  ; t1 := SortVar(tv1){TP()}
  ; (trm3, (st4, s2*)) := <insertCasts-MatchTerm(|e2, <current-type(|t1)> st3)> trm1
  ; if <type-var-lookup(|tv1);not(?SortVar(_) + oncetd(?DynT()))> st4
      then id
      else <report-Err> (trm1, MatchNotSpecificEnoughForTP(<current-type> st3))
    end
  ; t3 := <current-type> st4
  ; (cs2, st5)  := <thread-map-otf(insertCasts-RuleCond(|e2))> (cs1, st4)
  ; cs3 := <RuleDef-after-match> ([s1*, s2*], cs2)
  ; (trm4, st6) := <insertCasts-BuildTerm(|e2, st5, t3)> trm2
  ; st7 := <type-var-elim(id|[tv1, tv2])> st6
  ; r2 := <otf(!Rule(trm3, trm4, cs3))> r1

insertCasts-RuleDef-(|e1, st1):
  d@RDefP(x, s1*, t1*, r1@Rule(trm1, trm2, cs1)) -> (RDefP(x, s1*, t1*, r2), st7)
  where
    (FunTType(st*, tt*, t1, t2), st2) := <strat-lookup-def(|x{}, <length> s1*, <length> t1*, st1)> e1
  with
    s2* := <map(?DefaultVarDec(<id>) <+ ?VarDec(<id>, _))> s1*
  ; e2 := <strat-merge-env(keep-right)> (e1, <strat-zip-new-env> (s2*, <map(\st -> st{LocalDef()}\)> st*))
  ; (t2*, (st3, s1*)) := <thread-map-otf(insertCasts-MatchTerm(|e2))> (<zip> (t1*, tt*), (st2, []))
  ; tv := "def"
  ; (trm3, (st4, s2*)) := <insertCasts-MatchTerm(|e2, <current-type(|t1)> st3)> trm1
  ; t3 := <current-type> st4
  ; (cs2, st5)  := <thread-map-otf(insertCasts-RuleCond(|e2))> (cs1, st4)
  ; cs3 := <RuleDef-after-match> ([s1*, s2*], cs2)
  ; (trm4, st6) := <insertCasts-BuildTerm(|e2, st5, t2)> trm2
  ; st7 := <type-var-elim(id|tv)> st6
  ; r2 := <otf(!Rule(trm3, trm4, cs3))> r1

RuleDef-after-match: ([], cs) -> cs
RuleDef-after-match: (s*, cs) -> [<otf(!WhereClause(s))> s | cs] where s := <seqs> s*

insertCasts-RuleCond(|e1) = otf22(insertCasts-RuleCond-(|e1))

insertCasts-RuleCond-(|e1):
  (WhereClause(s1), st1) -> (WhereClause(s2), st3)
  with
    (s2, st2) := <insertCasts-Strategy(|e1, st1)> s1
  ; st3 := <current-type-of(|st1)> st2

insertCasts-RuleCond-(|e1):
  (WithClause(s1), st1) -> (WithClause(s2), st3)
  with
    (s2, st2) := <insertCasts-Strategy(|e1, st1)> s1
  ; st3 := <current-type-of(|st1)> st2
