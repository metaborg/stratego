module specialize

imports
  strj/multimatch/dfa
  strj/multimatch/matrix

  signatures/core/terms-sig

strategies
  /*
   * Specialize the matrix to a certain constructor.
   */
  specialize(|Matrix(rows)): cons -> Matrix(<filter(specialize-row(|cons))> rows)

//  // For debugging purposes
//  specialize-row'(|cons) = id
//  ; where(!(cons, <id>); debug(!"[i] spec-row ")) // Print cons and matrix
//  ; specialize-row(|cons); debug(!"[o] spec-row ") // Call original strategy
//  specialize-row(|c): Row(ps, A) -> Row(<specialize-pats(|c)> ps, A)
  specialize-row(|c) = Row(specialize-pats(|c), id)

  // Annotations
  specialize-pats(|Anno()):    [Anno(c, ann) | ps*] -> [c, ann | ps*]
  specialize-pats(|Anno()):    [Wld() | ps*] -> <prepend-wld(|2)> ps*

  // Constructor applications
  specialize-pats(|Fun(f, a)): [Op(f, qs*) | ps*] -> [qs*, ps*]
    where <eq> (a, <length> qs*)
  specialize-pats(|Fun(_, a)): [Wld() | ps*] -> <prepend-wld(|a)> ps*

  // Literals
  specialize-pats(|Str(s)):    [<?Str(s) + ?Wld()> | ps*] -> ps*
  specialize-pats(|Int(s)):    [<?Int(s) + ?Wld()> | ps*] -> ps*
  specialize-pats(|Real(s)):   [<?Real(s) + ?Wld()> | ps*] -> ps*

  prepend-wld(|n) = repeat(![Wld() | <id>] | n)
