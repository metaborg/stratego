module decompose

imports
  strj/multimatch/dfa
  strj/multimatch/matrix

  signatures/core/strategies-sig
  signatures/core/terms-sig

strategies
  /*
   * Determine default case for matrix.
   *
   * Maranget (2008), fig. 1.
   */
  default: Matrix(rows) -> Matrix(<filter(default-row)> rows)

  default-row: Row([Wld() | ps], a) -> Row(ps, a)
  default-row = ?Row([Fun(_, _) | _], _); fail // by definition

strategies
  /*
   * Get head constructors from list of (constructor) patterns.
   *
   * Maranget (2008), sec 4, step 3a.
   */
  head-constructors = filter(head-constructor-arity)
  head-constructor-arity: c -> (<head-constructor> c, <arity> c)

  /*
   * Specialize the matrix to a certain constructor.
   *
   * Maranget (2008), fig. 1.
   */
  specialize(|Matrix(rows)): cons -> Matrix(<filter(specialize-row(|cons))> rows)
  specialize-row(|c) = Row(specialize-pats(|c), id)

  specialize-occs(|a): occs@[o | os*] -> [o*, os*]
    with
      <debug(!"a / occs ")> (a, occs) 
    ; o* := <debug(!"[i] specocc " ); specialize-occ(|a); debug(!"[o] specocc " )> o

  specialize-occ(|a):     p@Path(_) -> ps
    with ps := <range; map(\ i -> <append-occ> (p, Sub(<int-to-string> i)) \)> (1, <inc> a)

  append-occ: (Path(os*), o) -> Path([os*, o*])
    with o* := [o]

  // Annotations
  arity:                        Anno(_, _)          -> 2
  head-constructor:             Anno(c, ann)        -> Anno()
  specialize-pats(|Anno()):    [Anno(c, ann) | ps*] -> [c, ann | ps*]
  specialize-pats(|Anno()):    [Wld() | ps*]        -> <prepend-wld(|2)> ps*

  // Constructor applications
  arity:                        Op(f, ts)         -> <length> ts
  head-constructor:             c@Op(f, ts)         -> Fun(f, <arity; int-to-string> c)
  specialize-pats(|Fun(f, x)): [c@Op(f, qs*) | ps*] -> [qs*, ps*]
    where
      a := <string-to-int> x
    ; <eq> (a, <arity> c)
  specialize-pats(|Fun(_, x)): [Wld() | ps*]      -> <prepend-wld(|a)> ps*
    where a := <string-to-int> x

  // Literals
  arity:            <?Str(_) + ?Int(_) + ?Real(_)> -> 0
  head-constructor = ?Str(_) + ?Int(_) + ?Real(_)
  specialize-pats(|Str(s)):    [<?Str(s) + ?Wld()> | ps*] -> ps*
  specialize-pats(|Int(s)):    [<?Int(s) + ?Wld()> | ps*] -> ps*
  specialize-pats(|Real(s)):   [<?Real(s) + ?Wld()> | ps*] -> ps*

  // Variables
  arity = (?Var(_) + ?Wld()); fail
  head-constructor = (?Var(_) + ?Wld()); fail // by definition
  specialize-pats(|Wld()) = fail // by definition

  prepend-wld(|n) = repeat(![Wld() | <id>] | n)
