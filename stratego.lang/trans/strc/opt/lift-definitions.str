module lift-definitions
imports strc/lib/stratlib strc/opt/stratego-laws
strategies

  lift-definitions =
    {| TopLevel:
      declare-standard-strategies
    ; Specification([
        id
      , Strategies(
          map(ignore-anno-def(DeclareTopLevel))
        ; map(lift-sdefs <+ ![<id>])
        ; concat
        )
      ])
    |}

  declare-standard-strategies =
    where(
      <map({?f; rules(TopLevel : f -> ())})> 
      ["_id", "_fail"]
    )

  DeclareTopLevel =
    ?SDefT(f, xs, ys, s)
    ; rules(TopLevel : f -> ())

  DeclareTopLevel =
    ?ExtSDefInl(f, xs, ys, s)
    ; rules(TopLevel : f -> ())

  DeclareTopLevel =
    ?ExtTypedDefInl(f, xs, ys, _, s)
    ; rules(TopLevel : f -> ())

  DeclareTopLevel =
    ?ExtSDef(f, xs, ys)
    ; rules(TopLevel : f -> ())

  DeclareTopLevel =
    ?ExtTypedDef(f, _)
    ; rules(TopLevel : f -> ())

  lift-sdefs :
    AnnoDef(a*, SDefT(f, xs, ys, s)) -> <conc>(defs, [AnnoDef(a*, SDefT(f, xs, ys, s'))])
    where <collect-split(LiftFromLet)> s => (s', defs)

  lift-sdefs :
    SDefT(f, xs, ys, s) -> <conc>(defs, [SDefT(f, xs, ys, s')])
    where <collect-split(LiftFromLet)> s => (s', defs)

  LiftFromLet :
    Let(defs, s) -> (Let(defs2, s), defs1)
    where <partition(SuperCombinator)> defs => (defs1, defs2)
    ; <map(DeclareTopLevel)> defs1

  SuperCombinator =
    ?def@SDefT(f, xs, ys, s)
    ; where(
        <tvars> def => []
        ; <svars; filter(not(TopLevel))> def => []
      )
