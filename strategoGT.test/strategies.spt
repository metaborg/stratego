module strategies

language strategoGT

fixture [[
specification

signature
  constructors
    Foo : Bar

strategies
  [[...]]
]]

test fail strategy can be used in any typed strategy [[
int-id :: int -> int
int-id = fail

str-id :: string -> string
str-id = fail

bar-id :: Bar -> Bar
bar-id = fail
]] 0 errors
   0 warnings

test fail strategy can be used as any typed strategy [[
int-id(int -> int) :: ? -> ?
int-id(s) = s

str-id(string -> string) :: ? -> ?
str-id(s) = s

bar-id(Bar -> Bar) :: ? -> ?
bar-id(s) = s

testing =
  int-id(fail)
; str-id(fail)
; bar-id(fail)
]] 0 errors
   0 warnings

test id strategy can be used in any typed strategy [[
int-id :: int -> int
int-id = id

str-id :: string -> string
str-id = id

bar-id :: Bar -> Bar
bar-id = id
]] 0 errors
   0 warnings

test id strategy can be used as any typed identity strategy [[
int-id(int -> int) :: ? -> ?
int-id(s) = s

str-id(string -> string) :: ? -> ?
str-id(s) = s

bar-id(Bar -> Bar) :: ? -> ?
bar-id(s) = s

testing =
  int-id(id)
; str-id(id)
; bar-id(id)
]] 0 errors
   0 warnings

test proceed may not be used in a non-extend strategy 1 [[
foobar = [[proceed(|"a")]]
]] 1 error at #1
   0 warnings

test proceed may not be used in a non-extend strategy 2 [[
foobar = [[proceed]]
]] 1 error at #1
   0 warnings

test proceed should use the arity of the strategy it's in 1 [[
extend foobar = [[proceed(|"a")]]
]] 1 error at #1
   0 warnings

test proceed should use the arity of the strategy it's in 2 [[
extend foobar(|a) = [[proceed]]
]] 1 error at #1
   0 warnings

test proceed in extend [[
extend foobar = proceed
]] 0 errors
   0 warnings

test proceed in override [[
override foobar = proceed
]] 0 errors
   0 warnings


