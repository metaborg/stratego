module stratego/sugar/rules

  imports 
    stratego/core/identifiers
    stratego/core/signatures
    stratego/core/terms
    stratego/sugar/terms
    stratego/sugar/strategies

  sorts RuleDef
  
  templates
    RuleDef.RDefNoArgs = 
      <<Id> : 
         <Rule>>
    
    Def.AnnoDef = 
      <<Anno+> <RuleDef>>
    
    RuleDef.RDef = 
      <<Id>(<Typedid*; separator=", ">) : 
         <Rule>>
    
    RuleDef.RDefT = 
      <<Id> ( <Typedid*; separator=", "> | <Typedid*; separator=", ">) : 
         <Rule>>

  sorts Rule RuleCond
  
  templates
    Rule.RuleNoCond = [[Term] -> [Term]] // backward comp.
    
    Rule.Rule = [[Term] -> [Term] where [Strategy]]  // backward comp.
    
    Rule.Rule = [[Term] -> [Term] [RuleCond+]] {avoid}
    
    RuleCond.WhereClause = <where <Strategy>>
    RuleCond.WithClause = <with <Strategy>>
  

